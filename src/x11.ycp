/**************
FILE          : x11.ycp
***************
PROJECT       : YaST2
              :
AUTHOR        : Marcus Schäfer <ms@suse.de>
              :
BELONGS TO    : YaST2
              : (X11 integration part using SaX2/ISaX)
              :
DESCRIPTION   : This script is a wrapper for x11main.ycp
              : defining what to do after the main dialog
              : is finished
              :
STATUS        : Development
**************/
{
textdomain "x11";

import "Display";
import "Wizard";
import "X11Version";

include "ui/common_popups.ycp";

//==========================================
// Globals...
//------------------------------------------
any ret = nil;

//==========================================
// Global Message Strings
//------------------------------------------
// Indicate that some packages required to setup
// X11 are missing
// ---
string pacsMissing = _("
The following packages are missing.
%1
Install now ?
");
// Indicate that the installed graphics card require
// XFree86 3.3.x to work properly
// ---
string versionMismatch = _("
Your graphics card required XFree86 V3 to be used
");

//==========================================
// Functions...
//------------------------------------------
//---[ finish ]----//
define any finish() ``{
	any localReturn = `next;
	UI::BusyCursor();
	SCR::Execute (.target.bash, "/sbin/SuSEconfig --module xdm");
	SCR::Execute (.target.bash, "/sbin/SuSEconfig --module kdm3");
	SCR::Execute (.target.bash, "/sbin/SuSEconfig --module gdm");
	UI::NormalCursor();
	return localReturn;
}

//==========================================
// installPackages()...
//------------------------------------------
define any installPackages() ``{
	any localReturn = `next;
	list neededPacs = [];
	list requiredPacs = [
		"xf86", "xloader", "xmodules", "mesasoft",
		"3ddiag", "sax2", "saxident"
	];
	// ... /
	// setup contents of requiredPacs plus required pacs
	// for the detected card(s)
	// ---
	requiredPacs = toset (
		flatten( [requiredPacs, Display::pacs_to_install] )
	);
	// ... /
	// create sublist of required packages which are
	// NOT installed at the time
	// ---
    foreach (`pac, requiredPacs, ``{
	if ( ! SCR::Read(.targetpkg.installed, pac) ) {
		neededPacs = add ( neededPacs, pac );
	}
	});
	// ... /
	// if there are packages left in the neededPacs list
	// we need to install them now. ask the user for
	// installing these packages
	// ---
	if ( size( neededPacs ) > 0 ) {
		string missing = "";
		foreach (`pac, neededPacs, ``{
			missing = missing + pac + "\n";
		});
		if ( ! UI::YesNoPopup ( sformat (
			pacsMissing,
			missing
		))) {
			localReturn = nil;
		} else {
			if ( WFM::CallFunction ( `sw_single( neededPacs ) ) != `next) {
			localReturn = nil;
			}
		}
	}
	return (
		localReturn
	);
}

//==========================================
// Check if X11 is installed
//------------------------------------------
if ( ! X11Version::have_x11 ) {
	return nil;
}

//==========================================
// check XFree86 version
//------------------------------------------
if ( X11Version::version == "3" ) {
	UI::ErrorPopup ( versionMismatch );
    return nil;
}

//==========================================
// install eventually missing packages...
//------------------------------------------
if (installPackages() == nil) {
	return( nil );
}

//==========================================
// Save touched configuration files...
//------------------------------------------
SCR::Execute (.target.bash,
	"/bin/cp /etc/X11/XF86Config /etc/X11/XF86Config.YaST2save"
);
if (SCR::Read(.sysconfig.bootloader.LOADER_TYPE) == "lilo") {
	SCR::Execute (.target.bash,
		"/bin/cp /etc/lilo.conf /etc/lilo.conf.YaST2save"
	);
}

//==========================================
// call x11main.ycp
//------------------------------------------
ret =  WFM::CallFunction (
	`x11main( true, true )
);
if ( ret == `next ) {
	ret = finish();
}
return ret;
}
